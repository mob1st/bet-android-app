import br.com.mob1st.buildsrc.JacocoConstantsKt

apply plugin: "org.sonarqube"

def p = new Properties()
p.load(project.rootProject.file('local.properties').newDataInputStream())

sonarqube {
    properties {
        property "sonar.login", System.hasProperty("SONAR_TOKEN") ? System.getenv("SONAR_TOKEN") : p.getProperty('sonar.token')
        property "sonar.projectKey", "mob1st_bet-app-android"
        property "sonar.organization", "mob1st"
        property "sonar.projectName", "bet-app-android"
        property "sonar.host.url", "https://sonarcloud.io/"
        property "sonar.java.coveragePlugin", "jacoco"
        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.coverage.jacoco.xmlReportPaths", "$buildDir/reports/jacoco/jacocoFullReport.xml"
        property "sonar.coverage.exclusions", JacocoConstantsKt.excludedFiles()
    }
}

subprojects { project ->
    apply plugin: "org.sonarqube"

    sonarqube {
        properties {
            if (!JacocoConstantsKt.isModuleExcluded(project)) {
                property "sonar.junit.reportPaths", getJUnitReportPath(project)
            }
        }
    }
}

// In order to show number of unit tests from all modules, we need to specify the test paths of
// Android modules as Sonar only supports non-Android modules
// Related issue: https://community.sonarsource.com/t/kotlin-junit-tests-report-is-missing/3653
private static String getJUnitReportPath(Project project) {
    if (JacocoConstantsKt.isAndroidModule(project)) {
        return "${project.buildDir}/test-results/testDebugUnitTest"
    } else {
        return "${project.buildDir}/test-results/test"
    }
}
